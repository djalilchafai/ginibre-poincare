module

public import GinibrePoincare.Analysis.GaussianFiniteIndexLSI

@[expose] public section

open MeasureTheory ProbabilityTheory
open scoped NNReal ContDiff
namespace GinibrePoincare
noncomputable section

theorem gaussianPi_lsi_bounded_C1 (v : ℝ≥0) (n : ℕ)
    (f : (Fin (n + 1) → ℝ) → ℝ) (hf : ContDiff ℝ 1 f)
    (C D : ℝ) (hC : 0 ≤ C) (hb : ∀ x, |f x| ≤ C)
    (hdf : ∀ x, ‖fderiv ℝ f x‖ ≤ D) :
    squareEntropy (Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v)) f ≤
      (2 * (v : ℝ)) * ∫ x, gaussianPiEnergy (n + 1) f x
        ∂Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v) := by
  let T := gaussianProductCoordinates n
  have hp := gaussianProductCoordinates_measurePreserving v n
  have hder (x : GaussianProductSpace n) : ‖fderiv ℝ (f ∘ T) x‖ ≤ D * ‖T.toContinuousLinearMap‖ := by
    rw [ContinuousLinearEquiv.comp_right_fderiv]
    exact (ContinuousLinearMap.opNorm_comp_le _ _).trans
      (mul_le_mul_of_nonneg_right (hdf _) (norm_nonneg _))
  have he := gaussianProduct_lsi_bounded_C1 v n (f ∘ T)
    (hf.comp T.contDiff) C (D * ‖T.toContinuousLinearMap‖) hC (fun x => hb (T x)) hder
  have hent : squareEntropy (Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v)) f =
      squareEntropy (gaussianProductLaw v n) (f ∘ T) := by
    rw [← hp.map_eq]
    exact squareEntropy_map _ _ T.continuous.measurable.aemeasurable f
      (hf.continuous.pow 2).aestronglyMeasurable
      (continuous_square_mul_log hf.continuous).aestronglyMeasurable
  have hm : Measurable (gaussianPiEnergy (n + 1) f) := by
    unfold gaussianPiEnergy
    apply Finset.measurable_sum
    intro i hi
    exact (measurable_fderiv_apply_const ℝ f _).pow_const 2
  have henergy : (∫ x, gaussianPiEnergy (n + 1) f x
        ∂Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v)) =
      ∫ x, gaussianProductEnergy n (f ∘ T) x ∂gaussianProductLaw v n := by
    rw [← hp.map_eq, integral_map T.continuous.measurable.aemeasurable hm.aestronglyMeasurable]
    exact integral_congr_ae (Filter.Eventually.of_forall
      (fun x => (gaussianProductEnergy_coordinates n f x).symm))
  rw [hent, henergy]
  exact he


theorem gaussianFiniteIndex_lsi_bounded_C1
    (I : Type*) [Fintype I] [DecidableEq I] [Nonempty I] (v : ℝ≥0)
    (f : (I → ℝ) → ℝ) (hf : ContDiff ℝ 1 f)
    (C D : ℝ) (hC : 0 ≤ C) (hb : ∀ x, |f x| ≤ C)
    (hdf : ∀ x, ‖fderiv ℝ f x‖ ≤ D) :
    squareEntropy (Measure.pi (fun _ : I => gaussianReal 0 v)) f ≤
      (2 * (v : ℝ)) * ∫ x, directionalEnergy (fun i : I => Pi.single i 1) f x
        ∂Measure.pi (fun _ : I => gaussianReal 0 v) := by
  classical
  obtain ⟨n, hn⟩ := Nat.exists_eq_succ_of_ne_zero (Fintype.card_ne_zero : Fintype.card I ≠ 0)
  let e : Fin (n + 1) ≃ I := (Fintype.equivFinOfCardEq hn).symm
  let T := ContinuousLinearEquiv.piCongrLeft ℝ (fun _ : I => ℝ) e
  have hp : MeasurePreserving T
      (Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v))
      (Measure.pi (fun _ : I => gaussianReal 0 v)) :=
    measurePreserving_piCongrLeft (fun _ : I => gaussianReal 0 v) e
  have hd (j : Fin (n + 1)) : T (Pi.single j 1) = Pi.single (e j) 1 := by
    funext i
    simp [T, ContinuousLinearEquiv.piCongrLeft, Equiv.piCongrLeft,
      Equiv.piCongrLeft', Pi.single_apply, Equiv.symm_apply_eq]
  have hder (x : Fin (n + 1) → ℝ) : ‖fderiv ℝ (f ∘ T) x‖ ≤ D * ‖T.toContinuousLinearMap‖ := by
    rw [ContinuousLinearEquiv.comp_right_fderiv]
    exact (ContinuousLinearMap.opNorm_comp_le _ _).trans
      (mul_le_mul_of_nonneg_right (hdf _) (norm_nonneg _))
  have he := gaussianPi_lsi_bounded_C1 v n (f ∘ T)
    (hf.comp T.contDiff) C (D * ‖T.toContinuousLinearMap‖) hC (fun x => hb (T x)) hder
  have hent : squareEntropy (Measure.pi (fun _ : I => gaussianReal 0 v)) f =
      squareEntropy (Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v)) (f ∘ T) := by
    rw [← hp.map_eq]
    exact squareEntropy_map _ _ T.continuous.measurable.aemeasurable f
      (hf.continuous.pow 2).aestronglyMeasurable
      (continuous_square_mul_log hf.continuous).aestronglyMeasurable
  have hm : Measurable (directionalEnergy (fun i : I => Pi.single i 1) f) := by
    unfold directionalEnergy
    exact Finset.measurable_sum _ (fun i hi => (measurable_fderiv_apply_const ℝ f _).pow_const 2)
  have henergy (x : Fin (n + 1) → ℝ) : gaussianPiEnergy (n + 1) (f ∘ T) x =
      directionalEnergy (fun i : I => Pi.single i 1) f (T x) := by
    unfold gaussianPiEnergy directionalEnergy
    rw [ContinuousLinearEquiv.comp_right_fderiv]
    simp only [ContinuousLinearMap.comp_apply, ContinuousLinearEquiv.coe_coe, hd]
    exact e.sum_comp (fun i : I => (fderiv ℝ f (T x) (Pi.single i 1)) ^ 2)
  have hi : (∫ x, directionalEnergy (fun i : I => Pi.single i 1) f x
        ∂Measure.pi (fun _ : I => gaussianReal 0 v)) =
      ∫ x, gaussianPiEnergy (n + 1) (f ∘ T) x
        ∂Measure.pi (fun _ : Fin (n + 1) => gaussianReal 0 v) := by
    rw [← hp.map_eq, integral_map T.continuous.measurable.aemeasurable hm.aestronglyMeasurable]
    exact integral_congr_ae (Filter.Eventually.of_forall (fun x => (henergy x).symm))
  rw [hent, hi]
  exact he


end
end GinibrePoincare
